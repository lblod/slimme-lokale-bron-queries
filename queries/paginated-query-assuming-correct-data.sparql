PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX ex: <http://www.example.org#>
PREFIX eli: <http://data.europa.eu/eli/ontology#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
SELECT DISTINCT
?subject  ?decision  ?title ?desc  ?datePublication  ?governingBody ?governingBodyLabel ?municipality  ?thema

WHERE {

  {
    SELECT  ?subject ?decision ?title ?desc ?datePublication ?governingBody ?governingBodyLabel ?municipality  ?thema
    WHERE {
      ?s a <http://data.vlaanderen.be/ns/besluit#Zitting>;
         <http://data.vlaanderen.be/ns/besluit#behandelt>/^<http://purl.org/dc/terms/subject> ?subject.
      ?subject prov:generated ?decision.

      ?decision eli:title ?title;
                eli:description ?desc;
                dcterms:subject ?thema;
                eli:date_publication ?datePublication.

     
        ?s <http://data.vlaanderen.be/ns/besluit#isGehoudenDoor> ?governingBody.
        ?governingBody skos:prefLabel ?governingBodyLabel.

        {
          ?governingBody <http://data.vlaanderen.be/ns/besluit#bestuurt> ?bt.
          ?bt <http://data.vlaanderen.be/ns/besluit#werkingsgebied> ?abstractLocation.
          ?abstractLocation rdfs:label ?abstractLocationLabel.
        } UNION {
          ?governingBody <http://data.vlaanderen.be/ns/mandaat#isTijdspecialisatieVan> ?tijd.
          ?tijd <http://data.vlaanderen.be/ns/besluit#bestuurt> ?bt.
          ?bt <http://data.vlaanderen.be/ns/besluit#werkingsgebied> ?location.
          ?location rdfs:label ?locationLabel.
        }

        BIND(COALESCE(?abstractLocationLabel, ?locationLabel) AS ?municipality)
      
    }
    ORDER BY ?datePublication
    LIMIT 50      
    OFFSET 0      
  }
}
